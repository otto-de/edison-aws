dependencies {
    compile project(":edison-aws-core")
    compile libraries.spring_boot_autoconfigure
    compile libraries.hibernate_validator
    compile libraries.aws_sdk_cloudwatch
    compile libraries.edison_jobs
    compile libraries.aws_sdk_dynamo, without_log4j

    testCompile test_libraries.dynamodb_local
    testCompile test_libraries.junit
    testCompile test_libraries.hamcrest_core
    testCompile test_libraries.hamcrest_library
    testCompile test_libraries.spring_boot_starter_test
}

repositories {
    maven { url 'https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release' }
}

// dynamodb local uses sql4java which uses a native lib. This has to be put on the java.library.path
task copyNativeDeps(type: Copy) {
    from(configurations.testCompile) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/libs'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/libs'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'edison-aws-dynamo'
                packaging 'jar'
                description 'CloudWatch metrics reporter library of the edison-aws project.'
                url 'http://github.com/otto-de/edison-aws'

                scm {
                    url 'scm:git@github.com:otto-de/edison-aws.git'
                    connection 'scm:git@github.com:otto-de/edison-aws.git'
                    developerConnection 'scm:git@github.com:otto-de/edison-aws.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsteinacker'
                        name 'Guido Steinacker'
                    }
                }
            }
        }
    }
}
