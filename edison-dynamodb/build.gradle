dependencies {
    compileOnly libraries.edison_jobs
    compileOnly libraries.edison_togglz

    compile libraries.edison_core
    compile libraries.spring_boot
    compile libraries.spring_boot_autoconfigure
    compile libraries.aws_sdk_dynamodb

    testCompile test_libraries.edison_testsupport
    testCompile libraries.edison_jobs
    testCompile libraries.edison_togglz
}

task startDB(type: Exec) {
    commandLine "bash", "-c", "docker run -p 8000:8000 -d tray/dynamodb-local -inMemory -sharedDb -port 8000"
}

task stopDB(type: Exec) {
    commandLine "bash", "-c", "id=\$(docker ps | grep \"tray/dynamodb-local\" | awk '{print \$1}');if [[ \${id} ]]; then docker stop \$id; fi"
}

test.dependsOn startDB
test.finalizedBy stopDB

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'edison-dynamodb'
                packaging 'jar'
                description 'dynamodb library of the edison-microservice project.'
                url 'http://github.com/otto-de/edison-microservice'

                scm {
                    url 'scm:git@github.com:otto-de/edison-microservice.git'
                    connection 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection 'scm:git@github.com:otto-de/edison-microservice.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsteinacker'
                        name 'Guido Steinacker'
                    }
                }
            }
        }
    }
}
